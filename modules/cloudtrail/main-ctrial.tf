#################################################
# S3 BUCKET FOR CLOUDTRAIL LOGS
#################################################
#tfsec:ignore:aws-cloudtrail-require-bucket-access-logging
# Reason: Access logging skipped for lab environment
resource "aws_s3_bucket" "cloudtrail_logs" {

  bucket = "${var.project_name}-${var.environment}-cloudtrail-logs"
  force_destroy = true

}
resource "aws_s3_bucket_versioning" "cloudtrail_logs_versioning" {

  bucket = aws_s3_bucket.cloudtrail_logs.id

  versioning_configuration {

    status = "Enabled"

  }

}
resource "aws_s3_bucket_public_access_block" "cloudtrail_logs_pab" {

  bucket = aws_s3_bucket.cloudtrail_logs.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true

}
resource "aws_s3_bucket_server_side_encryption_configuration" "cloudtrail_encryption" {

  bucket = aws_s3_bucket.cloudtrail_logs.id

  rule {

    apply_server_side_encryption_by_default {

      sse_algorithm = "AES256"

    }

  }

}
data "aws_caller_identity" "current" {}
#################################################
# S3 BUCKET POLICY FOR CLOUDTRAIL
#################################################

resource "aws_s3_bucket_policy" "cloudtrail_policy" {

  bucket = aws_s3_bucket.cloudtrail_logs.id

  policy = jsonencode({

    Version = "2012-10-17"

    Statement = [

      {

        Sid = "AWSCloudTrailAclCheck"

        Effect = "Allow"

        Principal = {

          Service = "cloudtrail.amazonaws.com"

        }

        Action = "s3:GetBucketAcl"

        Resource = aws_s3_bucket.cloudtrail_logs.arn

      },

      {

        Sid = "AWSCloudTrailWrite"

        Effect = "Allow"

        Principal = {

          Service = "cloudtrail.amazonaws.com"

        }

        Action = "s3:PutObject"

      Resource = "${aws_s3_bucket.cloudtrail_logs.arn}/AWSLogs/${data.aws_caller_identity.current.account_id}/*"

        Condition = {

          StringEquals = {

            "s3:x-amz-acl" = "bucket-owner-full-control"

          }

        }

      }

    ]

  })

}

#################################################
# CLOUDTRAIL
#################################################
#tfsec:ignore:aws-cloudtrail-ensure-cloudwatch-integration
# Reason: CloudWatch integration skipped for lab environment
resource "aws_cloudtrail" "main" {

  name = "${var.project_name}-${var.environment}-trail"

  s3_bucket_name = aws_s3_bucket.cloudtrail_logs.id

  include_global_service_events = true

  is_multi_region_trail = true

  enable_logging = true
  depends_on = [
  aws_s3_bucket_policy.cloudtrail_policy
]

}